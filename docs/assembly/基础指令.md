## 逻辑运算和移位指令
### 逻辑运算指令：AND，OR，XOR，NOT，TEST
* not:单目运算符 not ax =~ax
* test: 丢弃运算结果 ，保留标志位状态；本质上是and指令
```nasm
mov ax, 9234h
test ax, 8000h; ZF=0, AX=9234h
jnz msb_is_one; most significant bit最高位
;test和and的关系相当于cmp和sub的关系。
```
* 判断某个寄存器是否为0的几种方法:
```nasm
test cl, cl
or cl, cl
and cl, cl
or cl, 0
cmp cl, 0
```
#### 移位指令
* shl shr 逻辑左移、逻辑右移[针对非符号数]
* sal sar 算术左移、算术右移[针对符号数]
* sal=shl 均右边补零
* shr右移时左边永远补零， sar右移时左边补上元素的最高位 
```nasm
mov ah, 1011 0110B
rol ah, 1;  AH=0110 1101, CF=1
;sal: shift arithmetic left 算术左移
mov ah,11111100B;AH=-4
sar ah,2;AH=11111111 AH=-2
```
* sar对负数右移时左边要补1
* rcl: rotate through carry left 带进位循环左移
* rcr: rotate through carry right带进位循环右移
```nasm
mov ah,0B6h
clc       ;CF=0
rcl ah,1  ;CF=原来最高位的值 ah最地位会变成原CF的值
          ;ah=01101100 CF=1
          
mov ah, 0B6h
stc         ; CF=1
rcl ah, 1   ; CF=1 AH=1011 0110 移位前
            ; CF=1 AH=0110 1101 移位后
            
mov ah, 0B6h
stc         ; CF=1
rcr ah, 1   ; AH=1011 0110  CF=1移位前
            ; AH=1101 1011  CF=0移位后
            
mov ah,0B6h
stc
rcl ah, 1;  CF=1 AH=0110 1101
           ;dx   ax
```
* shl, shr, rol, ror, rcl, rcr 最后移出去的那一位一定在CF中。 

> 假定要把AX中的16位值转化成二进制输出:
* 解法1:
```nasm
mov cx, 16
next:
    shl ax, 1
    jc is_1

is_0:
    mov dl, '0'
    jmp output

is_1:
    mov dl, '1'

output:
push ax
mov ah, 2
int 21h
pop ax
dec cx
jnz next

```
* 解法2:
```nasm
mov cx, 16
next:
shl ax, 1
mov dl, '0'
adc dl, 0
output:
push ax
mov ah, 2
int 21h
pop ax
dec cx
jnz next
```


#### 字符串操作指令

#####字符串传送指令：MOVSB，MOVSW，MOVSD

* rep movsb : 其中rep表示repeat，s表示string，b表示byte

* 在执行此指令前要做以下准备工作：
  * ①ds:si->源字符串(si就是source index)
  * ②es:di->目标字符串(di就是destination index)
  * ③cx=移动次数
  * ④DF=0即方向标志设成正方向(用指令cld)
* rep movsb所做的操作如下:
```nasm
again:
if(cx == 0)
   goto done;
byte ptr es:[di] = byte ptr ds:[si]
if(df==0)
{si++; di++;}
else
{si--; di--;}
cx--
goto again
done:
```

* 例子: 要把以下左侧4个字节复制到右侧

```
1000:0000 'A'        2000:0000  'A'
1000:0001 'B'        2000:0001  'B'
1000:0002 'C'        2000:0002  'C'
1000:0003 00         2000:0003  00
```
```nasm
mov ax, 1000h
mov ds, ax
mov si, 0      ; mov si, 3
mov ax, 2000h
mov es, ax
mov di, 0      ; mov di, 3
mov cx, 4
cld             ; std
rep movsb
```

* rep movsw的操作过程:[word]
```nasm
again:
if(cx == 0)
   goto done;
word ptr es:[di] = word ptr ds:[si]
if(df==0)
{si+=2; di+=2;}
else
{si-=2; di-=2;}
cx--
goto again
done:
```
* rep movsd的操作过程:[dword]
```nasm
again:
if(cx == 0)
   goto done;
dword ptr es:[di] = dword ptr ds:[si]
if(df==0)
{si+=4; di+=4;}
else
{si-=4; di-=4;}
cx--
goto again
done:
```


* 在32位系统下, 假定`ds:esi->源内存块`, `es:edi->目标块`, `DF=0`, 则当要复制的字节数ecx不是4的倍数时，可以做如下处理:
```nasm
push ecx
shr ecx, 2
rep movsd
pop ecx
and ecx, 3; 相当于ecx = ecx % 4
rep movsb
```
#### 字符串比较指令：CMPSB，CMPSW，CMPSD

* cmpsb [jz je]
    * 比较byte ptr ds:[si]与byte ptr es:[di]
      * 当DF=0时，si++，di++
      * 当DF=1时，si--，di--
      * 自动对si di更新[为下一次移动准备]

* repe cmpsb
  * 若本次比较相等则继续比较下一个(repeat if equal)

```assembly
again:
if(cx == 0) goto done;
temp=byte ptr ds:[si]-byte ptr es:[di]
old_fl=FL
当DF=0时，si++，di++
当DF=1时，si--，di--
cx--
FL=old_fl
if (ZF==1) goto again
done:
```
* cx!=0 也可能提前结束循环
* repne cmpsb
  * 若本次比较不等则继续比较下一个(repeat if not equal)
```nasm
again:
if(cx == 0) goto done;
temp=byte ptr ds:[si]-byte ptr es:[di]
old_fl=FL
当DF=0时，si++，di++
当DF=1时，si--，di--
cx--
FL=old_fl
if (ZF==0) goto again
done:
```
#### 字符串扫描指令：scasb，scasw, scasd

* scasb:
```nasm
cmp al, es:[di]
di++; (当DF=1时，为di--)
```
* repne scasb:
```nasm
next:
  if(cx == 0) goto done;
  cmp al, es:[di]
      di++; 当DF=1时，为di--
      cx--
  je done
  goto next
done:
```

* 例子: 假定从地址1000:2000开始存放一个字符串，请计算该字符串的长度并存放到CX中。假定字符串以ASCII码0结束，字符串长度不包括0。
```nasm
mov ax, 1000h
mov es, ax
mov di, 2000h; ES:DI->目标串
mov cx, 0FFFFh; CX=最多找FFFF次
mov al, 0; AL=待找的字符
cld       ; DF=0，表示正方向
repne scasb; again:
not cx      ; 相当于cx=FFFF-cx
dec cx
;上述两条指令也可以替换成以下两条指令:
;inc cx
;not cx
;not cx相当于cx=FFFF-cx 
```
* repe scasb
    * 假定从地址1000:0000起存放以下字符串"###ABC"，现要求跳过前面的#，把后面剩余的全部字符复制到2000:0000中
    * 假定es=1000h, di=0, cx=7, 则


```nasm
mov al, '#'
cld
repe scasb
dec di; ES:DI->"ABC"
inc cx; CX=4
push es
pop ds; DS=ES
push di
pop si; SI=DI
mov ax, 2000h
mov es, ax
mov di, 0
rep movsb
```
#### 字符串操作指令
* stosb:
```nasm
es:[di] = AL
di++; DF=1时为di--
```
* rep stosb: 循环CX次stosb
```nasm
again:
if(cx == 0) goto done;
es:[di] = al
di++; 当DF=1时, 为DI--
cx--
goto again;
done:
```

```C
memset(void *t,int value,int n);
double x[100];
memset(&x,0,sizeof(x))
```

* lodsb
```nasm
al=ds:[si]
SI++;当DF=1时, 为SI--
```
* Exercise:
  * 设ds:si-> "##AB#12#XY"
  * es:di指向一个空的数组,
  * CX=11
  * 通过编程过滤#最后使得es:di ->"AB12XY"

```nasm
cld
again:
   lodsb ; AL=DS:[SI], SI++
         ; mov al, ds:[si]
         ; inc si
   cmp al, '#'
   je   next
   stosb ; ES:[DI]=AL, DI++
         ; mov es:[di], al
         ; inc di
next:
   dec cx
   jnz again

```

#### C语言字符串函数转换
```nasm
strlen:
    push bp
    mov bp,sp
    push di
    push ds


    mov di,[bp+4]
    mov ax,data
    mov ds,ax
    mov es,ax
    mov cx,0FFFFh
    xor al,al
    cld
    repne scasb
    inc cx
    not cx
    mov ax,cx
   
    pop ds
    pop di
    mov sp,bp
    pop bp
    ret
_memcpy:
    push bp
    mov bp,sp
    push es
    push ds
    push si
    push di
    mov ax,data
    mov es,ax
    mov ds,ax
    mov si,word ptr [bp+6]
    mov di,word ptr [bp+4]
    mov cx,word ptr [bp+8]
    cld
    rep movsb
    pop di
    pop si
    pop ds
    pop es
    mov sp,bp
    pop bp
    ret
_strncpy:
    push bp
    mov bp,sp
    push es
    push ds
    push si
    push di
    mov si, word ptr [bp+6]
    mov di,word ptr [bp+4]
    push si
    call _strlen
    pop cx
    cmp ax,[bp+8]
    jl tricky_form
    mov cx,word ptr [bp+8]
    cld
    rep movsb
    tricky_form:
    mov dx,[bp+8]
    sub dx,ax
    mov cx,ax
    cld
    rep movsb
    mov cx,dx
    mov ax,0
    cld
    rep stosb
    pop di
    pop si
    pop ds
    pop es
    mov sp,bp
    pop bp
    ret
_memset:
    push bp
    mov bp,sp
    push es
    push di
    mov ax,data
    mov es,ax
    mov di,[bp+4]
    mov ax,[bp+6]
    mov cx,[bp+8]
    cld
    rep stosb
    pop di
    pop es
    mov sp,bp
    pop bp
    ret
   
_clrscr:
    push es
    push di
    mov ax,0B800h
    mov es,ax
    mov di,0
    mov ax,0000h
    mov cx,2000
again:
    mov es:[di],ax
    add di,2
    sub cx,1
    jnz again
    pop di
    pop es
    ret
```
#### 控制转移指令
* jmp的3种类型
①jmp short target			; 短跳
②jmp near ptr target 	; 近跳
③jmp far ptr target  	; 远跳
* 一般情况下，编译器会自动度量跳跃的距离，因此我们在
写源程序的时候不需要加上short、near ptr、far ptr等类型修饰即上述三种写法一律可以简化为jmp target。
* 短跳指令[only one byte]
  * 格式 jmp + 偏移地址或标号
  * 以下条件跳转指令也都属于短跳: `jc jnc jo jno js jns jz jnz ja jb jae jbe jg jge jl jle jp jnp` 
  * 短跳指令的机器码
```
     地址            机器码   汇编指令  
    1D3E:0090     ...
    1D3E:00F0
    1D3E:0100     EB06     jmp  0108h
    1D3E:0102     B402     mov  ah，2
    1D3E:0104     B241     mov  dl, 41h
    1D3E:0106     CD21     int  21h
    1D3E:0108     B44C     mov  ah，4Ch
    1D3E:010A     CD21     int  21h
```
  * 短跳太远跳不过去的解决办法
```nasm
cmp ax, bx
（je  equal; jump out of range）
jne not_equal
jmp equal; 近跳
not_equal:
...; 假定这里省略指令的机器码总长度超过7Fh字节
equal:
...
```
* 近跳指令[two bytes]
  * 近跳指令的3种格式
```nasm 
jmp 偏移地址或标号        ; 如jmp 1000h
jmp 16位寄存器		; 如jmp bx
jmp 16位变量  		; 如jmp word ptr [addr]
```
* 远跳指令[跨段跳跃]
  * 远跳指令的2种格式
    * jmp 段地址:偏移地址
    * jmp dword ptr 32位变量
  * 远跳指令的机器码
    * `jmp 1234h:5678h`; 机器码为`0EAh,78h,56h,34h,12h `
* 远跳没有用△，而是直接地址.远跳到某个常数地址时,在源程序中不能直接用jmp指令，而应该改用机器码0EAh定义，如:
```nasm
db 0EAh
dw 5678h
dw 1234h
db 0EAh
dd 0FFFF0000h
;或 dw 0000h
;dw 0FFFFh
;上述3行定义合在一起表示jmp 1234h:5678h
```
* jmp dword ptr 32位变量的用法
```nasm
mov word ptr ds:[bx],1234h
mov word ptr ds:[bx+2],5678h
jmp dword ptr ds:[bx] ;jmp 5678h:1234h
```
```nasm
data segment
addr dw 0000h, 0FFFFh 
;或写成addr dd 0FFFF0000h
data ends
code segment
assume cs:code, ds:data
main:
mov ax, data
mov ds, ax
jmp dword ptr [addr] 
;相当于jmp FFFF:0000
code ends
end main
```


#### 循环指令：LOOP

* loop  dest的操作过程:
```nasm
CX = CX - 1   	; 循环次数减1
if(CX != 0)   	; 若CX不等于0，则
    goto  dest 	; 跳转至dest
```
* 例: 求1+2+3的和
```nasm
mov cx, 3
next:
add ax, cx; ax +3, +2, +1
loop next; cx=2, 1, 0
           ; dec cx
           ; jnz next  先-1再判断
           ;--cx初始值=0时能达到最大循环次数 10000h次

mov ax, 0
mov cx, 0
jcxz done  这条指令可以防止cx为0时进入循环
next:
add ax, cx
loop next; 循环10000h次
done:
```
* jecxz 表示jump if ecx is zero
#### call, ret指令
```assembly
近返回：retn，在多数情况下可写成ret
远调用：call far ptr dest
远返回：retf
```



#### 通用数据传送指令：MOV，PUSH，POP，XCHG

- mov byte ptr ds:[bx], byte ptr es:[di]
- 错误原因:两个操作数**不能同时为内存变量**以下为正确写法:

````nasm
mov al, es:[di]
mov ds:[bx], al
````


```assembly
eax=12345678h
ebx=89ABCDEFh
ss=1000h
sp=2000h
现执行以下指令:
push eax;(1)
push ebx;(2)
pop ebx;(3)
pop eax;(4)
```



* exchange 指令
````nasm
mov ax,1
mov bx,2
xchg ax,bx
xchg ax,ds:[bx] 可以交换寄存器和变量 不允许对两个变量进行交换
````
