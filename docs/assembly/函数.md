## 汇编语言中的三种参数传递方式
### 寄存器传递
```nasm
f:
   add ax, ax; ax=2*ax
   ret        ; 返回时ax就是函数值
main:
   mov ax, 3; ax就是f()的参数
   call f
next:
   mov ah, 4Ch
   int 21h
```
### 变量传递
```nasm
f:
   mov ax, var
   add ax, ax; ax就是函数值
   ret
main:
   mov var, 3
   ; var是一个16位的变量, 用作参数，var必须是全局变量
   call f
```
* 在汇编语言中，用 db、dw等关键词定义的变量均为全局变量 。在堆栈中定义的变量才是局部变量。
* var不能用于递归函数内--每一层变量地址各不相同，全局变量的话永远是同一个地址，所以不能用来传递递归函数的参数
### 堆栈传递
```nasm
f:
   push bp
   mov bp, sp
   mov ax, [bp+4]; 从堆栈中取得参数
   add ax, ax
   pop bp
   ret
main:
   mov ax, 3
   push ax; 参数压入到堆栈
   call f
   add sp, 2
```
* C语言函数调用y=f(2,3)求两数之和转化成汇编语言
* 设ss=1000h sp=2000h
```nasm
f:
   push bp;(4)
   mov bp, sp
   mov ax, [bp+4]
   add ax, [bp+6]
   pop bp; (5)
   ret; (6)  pop ip
main:
   mov ax, 3
   push ax; (1)
   mov ax, 2
   push ax; (2)
   call f; (3) 
   ;call会把f指令的下一条的地址（here）压入堆栈 然后再jmpf
here:
   add sp, 4;(7);堆栈恢复平衡
```
* 上述程序运行过程中的堆栈布局如下:stack frame
```
ss:1FF8 old bp <- bp (4)
ss:1FFA here <- (3)(5)
ss:1FFC 02   <- (2)(6)
ss:1FFE 03   <- (1)
ss:2000 ??   <- (7)
```
* printf函数原型
```C
#include <stdio.h>
double f(char *s, ...)
{  double y=0;
   char *p = (char *)&s; /* p = bp+4 */
   p += sizeof(s); /* p = bp+6 */
   while(*s != '\0')
   {
      if(*s == 'i')
      {
         y += *(int *)p;
         p += sizeof(int);
      }
      else if(*s == 'l')
      {
         y += *(long*)p;
         p += sizeof(long);
      }
      else if(*s == 'd')
      {
         y += *(double *)p;
         p += sizeof(double);
      }
      s++;
   }
   return y;
}
main()
{
   double y;
   y = f("ild", 10, 20L, 3.14);
   printf("y=%lf\n", y);
}
```
* call far ptr dest retf
```
1000:2000 call 1234:5678  ;跨段跳远
cpu会做一下动作：
[1]push cs
[2]push 2005h
[3]jmp 1234:5678h
;一定先压入段地址，再压入偏移地址  WHY?5.18最后一段
1000:2005 mov ah,4Ch
1234:5678 ...
1234:5688 retf
cpu 在retf时会做以下动作
[1]pop ip;
[2]pop cs;
```
### 用堆栈传递参数有3种方式:
####  __cdecl 
* 参数从**右到左顺序压入堆栈**，由调用者清理堆栈是C语言参数传递规范。
>__cdecl的例子:
```nasm
f:
push bp; (4)
mov bp, sp
mov ax, [bp+4]; arg0
add ax, [bp+6]; arg1
pop bp; (5)
ret; (6)
main:
mov ax, 20; arg1
push ax; (1)
mov ax, 10; arg0
push ax; (2)
call f; (3)
here:
add sp, 4; (7)
```
* __cdecl堆栈布局:
```
ss:1FF8 old bp -> bp (4)
ss:1FFA here -> (3)(5)
ss:1FFC 10 -> (2)(6)
ss:1FFE 20 -> (1)
ss:2000    -> (7)
```
#### __pascal
* 参数从左到右顺序压入堆栈，由**被调用者清理堆栈**是Pascal语言参数传递规范。
>__pascal的例子：
```nasm
f:
push bp; (4)
mov bp, sp
mov ax, [bp+6]; arg0
add ax, [bp+4]; arg1
pop bp; (5)
ret 4; (6)
main: 
mov ax, 10
push ax; (1) arg0
mov ax, 20
push ax; (2) arg1
call f; (3)
here: 
```
* __pascal的堆栈布局:
```
ss:1FF8 old bp<- bp (4)
ss:1FFA here <- (3)(5)
ss:1FFC 20 <- (2)
ss:1FFE 10 <- (1)
ss:2000    <-(6)
```
> ret 4:
> [1]pop ip
> [2]sp=sp+4
#### __stdcall
* 参数从**右到左**顺序压入堆栈，由**被调用者**清理堆栈是Windows API函数的参数传递规范。
> __stdcall的例子：
```nasm
f:
push bp; (4)
mov bp, sp
mov ax, [bp+4]; arg0
add ax, [bp+6]; arg1
pop bp; (5)
ret 4; (6)
main:
mov ax, 20
push ax; (1) arg1
mov ax, 10
push ax; (2) arg0
call f; (3)
here: 
```

   __stdcall的堆栈布局:
```
ss:1FF8 old bp<- bp (4)
ss:1FFA here <- (3)(5)
ss:1FFC 10 <- (2)
ss:1FFE 20 <- (1)
ss:2000    <-(6)
```
* 32位__stdcall的例子:
```nasm
f:
push ebp;(4)
mov ebp, esp
mov eax,[ebp+8];arg0
add eax,[ebp+0Ch];arg1
pop ebp;(5)
ret 8;(6)
main:
push 20;(1) arg1
push 10;(2) arg0
call f;(3)
here:
```
* 32位__stdcall的堆栈布局:
```
ss:1FF0 old ebp<- ebp (4)
ss:1FF4 here <- (3)(5)
ss:1FF8 10 <- (2)
ss:1FFC 20 <- (1)
ss:2000    <-(6)
```
### 动态变量
```C
int f(int a, int b)
{
   int c; /* c是局部动态变量 */
   c = a+b;
   return c;
}
```
>上述C语言函数可翻译成以下汇编代码:
```nasm
f:
push bp; (4)
mov bp, sp
sub sp, 2; (5) 这里挖的坑就是给变量c的
mov ax, [bp+4]
add ax, [bp+6]
mov [bp-2], ax
mov ax, [bp-2]
mov sp, bp; (6)此时变量c死亡
pop bp; (7)
ret; (8)
main:
mov ax, 20
push ax; (1)
mov ax, 10
push ax; (2)
call f; (3)
here:
add sp, 4;(9)此时参数a,b死亡
```
>执行上述代码时, 堆栈布局如下:
```
ss:1FF6 [30] (5) 变量c
ss:1FF8 old bp<- bp(4)(6)
ss:1FFA here <- (3)(7)
ss:1FFC 10 <- (2)(8)
ss:1FFE 20 <- (1)
ss:2000    <-(9)
```

#### C语言函数中需要保护bp, bx, si, di
* C语言的函数里面除了不能破坏bp外，还要保护bx,si,di的值:
```nasm
f:
push bp
mov bp, sp
sub sp, n; 其中n一个常数,用来为动态变量分配空间
           先挖坑再push
push bx
push si
push di
...
pop di
pop si
pop bx
mov sp, bp
pop bp
ret
```
* 如果没有push bp --- 多层调用会出问题：main调用f f调用g 这时g->f->main回不去
### 递归
```C
int f(int n)
{
   if(n==1)
      return 1;
   return n+f(n-1);
}
```
>上述C语言递归函数可翻译成以下汇编代码:
```nasm
f:
    push bp;(3)(6)(9)
    mov bp, sp
    mov ax, [bp+4]
    cmp ax, 1
    je done
    dec ax
    push ax;(4)(7)
    call f;(5)(8)
there:
    add sp, 2;(12)(15)
    add ax, [bp+4]
done:
    pop bp;(10)(13)(16)
    ret;(11)(14)(17)
main:
    mov ax, 3
    push ax;(1)
    call f;(2)
    here:
    add sp, 2;(18)
```
> 执行上述代码时的堆栈布局如下:
```
ss:1FEE oldbp<-bp(9)
ss:1FF0 there<-(8)(10)
ss:1FF2 1<-(7)(11)
ss:1FF4 oldbp<-bp(6)12
ss:1FF6 there<-(5)(13)
ss:1FF8 2<-(4)(14)
ss:1FFA oldbp<-bp(3)(15)
ss:1FFC here <-(2)(16)
ss:1FFE 3 <-(1)(17)
ss:2000   <-(18)
```
