

### int16

* int 16h/ah=00h 属于bios中断
> int 16h ax=所敲键的编码
```nasm
again:  [刷新游戏的画面]
mov ah,1
int 16h 检查键盘缓冲区是否为空，若为空，则返回zf=1,否则返回zf=0  (ZF=1 jz跳转)
jz no_key
has_key:
    mov ah,0
    int 16h  从键盘的缓冲区读取一个键，根据所敲的键执行不同分支
no_key:
    jmp again
```

例子：
```nasm    
data segment
x dw 0
y dw 0
data ends
code segment
assume cs:code,ds:data
main:
  mov ax,data
  mov ds,ax
  mov ah,0
  mov al,3
  mov ax,0B800h
  mov es,ax
draw:
  ;bx=(y*80+x)*2
  mov ax,[y]
  mov bp,80
  mul bp 
;  dx(高16位):ax（低16位）=ax*bp 
;  其中dx=0 mul的乘数宽度16 则被乘数为ax
;  24x79总之不超过16位      
  add ax,[x]
  add ax,ax
  mov bx,ax
  mov es:[bx],1720h
 check_key:
  mov ah,1
  int 16h
  jnz has_key
  jmp check_key
 has_key:
  mov ah,0
  int 16h
  cmp ax,4800h
  je is_up
  cmp ax,5000h
  je is_down
  cmp ax,4B00h
  je is_left
  cmp ax,4D00h
  je is_right
  jmp exit
is_up:
  cmp [y],0
  je check_key
  dec [y]
  jmp draw
is_down:
  cmp [y],24
  je check_key
  inc [y]
  jmp draw
is_left:
  cmp [x],0
  je check_key
  dec [x]
  jmp draw
is_right:
  cmp [x],79
  je check_key
  inc [x]
  jmp draw
exit:
  mov ah,4Ch
  int 21h
code ends
end main
```

### int 21h
* int21h对应的函数首地址保存在0:84h处, 该地址是一个远指针。
```
0:84 78h
0:85 56h
0:86 34h
0:87 12h
xor ax,ax
mov es,ax
mov bx,84h
lds si,es:[bx]
;ds=1234h si=5678h
```
* dword ptr 0:[84h]称为int 21h的中断向量(其实是它的函数首地址)
>* int n对应的中断向量的存储位置一定是0:n*4
>* n的取值范围是[00, FF], 所以256个中断向量会占用0:0~0:3FF之间共400h个字节的内存，这块区域称为
中断向量表。

```
显然, int 00h的中断向量保存在dword ptr 0:[0];
int 01h的中断向量保存在dword ptr 0:[4];
int 03h的中断向量保存在dword ptr 0:[0Ch];
int 08h的中断向量保存在dword ptr 0:[20h];
int 0FFh的中断向量保存在dword ptr 0:[3FCh];
```
* BIOS会完成部分中断向量的填写, 如int 10h、int 16h、int 13h这几个BIOS中断的向量在DOS启动前就已经填好了
* DOS启动完成后，会填入int 21h的中断向量。

```
1000:2000 mov ah, 2
1000:2002 mov dl, 'A'
1000:2004 int 21h
1000:2006 mov ah,4Ch
```
* 上面这条int 21h指令执行时, cpu做了以下4件事:
```
IF=0
TF=0  -防止中断里再次产生中断
pushf
push cs即1000h
push 下条指令的偏移地址即2006h
jmp dword ptr 0:[84h]; jmp 1234h:5678h

上面这条jmp会跳转到int 21h的中断服务函数内部:
1234:5678  ...
             ...
1234:56FF  iret; 中断返回
iret在执行时, cpu做了以下3件事情:
pop ip即ip=2006h
pop cs即cs=1000h
popf

1000:2000 call 1234:5678; 远调用
;此时会push 1000h 再 push 2005h
;最后jmp 1234:5678
1000:2005 mov ah, 4Ch
1000:2007 int 21h
...
1234:5678 ...
            retf; 此时执行pop ip再pop cs
```

> 中断程序例子
```nasm
code segment
assume cs:code
old_80h dw 0, 0
main:
   xor ax, ax
   mov es, ax
   mov bx, 80h*4; mov bx, 200h
   mov ax, es:[bx]
   mov old_80h[0], ax
   mov ax, es:[bx+2]
   mov old_80h[2], ax
   
   mov word ptr es:[bx], offset int_80h
   mov es:[bx+2], cs
   
   mov ah, 1
   int 80h; AL=键盘输入的ASCII码
next:
   mov ah, 2
   mov dl, al
   int 80h
   
   mov ax, old_80h[0]
   mov es:[bx], ax
   mov ax, old_80h[2]
   mov es:[bx+2], ax
   
   mov ah, 4Ch
   int 21h
int_80h: ; ISR(Interrupt Service Routine)
         ; 中断服务函数
   cmp ah, 1
   je is_1
is_2:
   push es
   push bx
   push ax
   mov bx, 0B800h
   mov es, bx
   mov byte ptr es:[160], dl
   mov byte ptr es:[161], 17h
   pop ax
   pop bx
   pop es
   jmp done
is_1:
   int 21h
done:
   iret
   ;如果想接连执行old int80：jmp dword ptr cs:[old_80h]

code ends
end main

```
